---
heroImage: /src/assets/images/cover_effect_eyeball.png
category: 实践记录
description: 基于Unity的真实眼球渲染效果
pubDate: 2025-02-28T16:00:00.000Z
tags:
  - 渲染效果
title: 眼球渲染
---

import LocalVideo from '../../components/LocalVideo.astro'

<LocalVideo src='/assets/Effects/Eyeball/video1.mp4' alt='video1' />

### 眼球渲染理论

#### 眼球结构介绍

<img
	src='/assets/Effects/Eyeball/img1.png'
	alt='img1'
	style='width: 100%; height: auto; display:block; margin: 0 auto;'
/>
<br></br>
<img
	src='/assets/Effects/Eyeball/img2.png'
	alt='img1'
	style='width: 50%; height: auto; display:block; margin: 0 auto;'
/>

1. **巩膜（sclera)** ： 眼白部分，通常非常湿润，包含少量的触感纹理、血丝等细节
2. **角膜缘（limbus）** ：角膜缘存在于虹膜和巩膜之间的深色环形
3. **虹膜（iris）** ： 眼仁彩色色素的部分，虹膜有收缩和扩张结构
4. **瞳孔（pupil）** ： 眼仁中心黑点
5. **角膜（cornea）** ： 覆盖在眼球表面的透明薄层，会反射场景中物体

<br></br>
#### 眼球模型

眼球并不是正半球体，而是稍微突起的水滴状

<img
	src='/assets/Effects/Eyeball/img3.png'
	alt='img3'
	style='width: 100%; height: auto; display:block; margin: 0 auto;'
/>

---

眼球的UV沿y轴投影展开

<img
	src='/assets/Effects/Eyeball/img4.png'
	alt='img3'
	style='width: 50%; height: auto; display:block; margin: 0 auto;'
/>

<br></br>
#### 眼球的渲染步骤

**~~关键效果~~**

需要着重实现的眼球效果：

- *角膜*的*光泽反射*效果
- 虹膜的*颜色*变化
- _虹膜的缩放_
- _瞳孔的缩放_
- 瞳孔的*次表面散射*
  - 光线穿过角膜表面层，产生了折射后，到达了虹膜

**~~角膜的光泽反射效果~~**

<img
	src='/assets/Effects/Eyeball/img5.png'
	alt='img5'
	style='width: 50%; height: auto; display:block; margin: 0 auto;'
/>

通过视线沿眼球表面法线反射向量 _采样Cubemap_ 以实现镜面反射效果

为了强调眼仁处的高光，可以叠加 _Blinn-Phong_ 镜面反射

<br></br>
**~~虹膜的颜色变化~~**

<img
	src='/assets/Effects/Eyeball/img6.png'
	alt='img6'
	style='width: 40%; height: auto; display:block; margin: 0 auto;'
/>

通过虹膜 _灰度_ 贴图乘以颜色来调整虹膜颜色

<br></br>
**~~虹膜的缩放~~**
<img
	src='/assets/Effects/Eyeball/img7.png'
	alt='img6'
	style='width: 100%; height: auto; display:block; margin: 0 auto;'
/>

*巩膜*与*虹膜*分别为两张贴图，通过*虹膜遮罩*区分贴图显示

通过\*缩放

<br></br>
**~~瞳孔的缩放~~**

由于瞳孔和虹膜集成在同一张贴图上，缩放的实现思路不能和虹膜的缩放一样

可以通过*重映射采样瞳孔区域贴图的UV*来实现瞳孔的缩放

<br></br>
**~~瞳孔的次表面散射~~**

<img
	src='/assets/Effects/Eyeball/img8.png'
	alt='img6'
	style='width: 70%; height: auto; display:block; margin: 0 auto;'
/>

由于光线在经过角膜后会发生折射，导致采样的位置偏移。

可以通过*基于物理的折射*来偏移UV，实现*视差*效果

### 具体实现

#### 漫反射

通过Lambert实现漫反射效果

```csharp
half4 frag(Varyings IN) : SV_Target
{
    half4 color;

    // 漫反射 -------------------------------------
    float3 normalWS = normalize(IN.normalWS);
    Light mainLight = GetMainLight();
    float3 lightDir = normalize(mainLight.direction);
    float NdotL = saturate(dot(normalWS, lightDir)) * 0.5 + 0.5;
    float3 diffuse = mainLight.color * NdotL;
}
```

<img
	src='/assets/Effects/Eyeball/img9.png'
	alt='img9'
	style='width: 100%; height: auto; display:block; margin: 0 auto;'
/>

<br></br>
#### 镜面反射

**Cube实现镜面反射**

- 通过计算视线沿表面法向量的反射向量对Cubemap进行采样

```csharp
  // Cubemap
half3 viewDirWS = normalize(_WorldSpaceCameraPos.xyz - IN.positionWS.xyz);
half3 reflectionUV = reflect(-viewDirWS, IN.normalWS);
```

---

- 为了能更加自由地旋转cubemap，增加了 `RotateCubemap()` 来通过自定义角度分别构建沿xyz的旋转矩阵，

```csharp
reflectionUV = RotateCubemap(reflectionUV, _ReflectionRotation);
half4 reflectionTex = SAMPLE_TEXTURECUBE(_CubeMap,sampler_CubeMap,reflectionUV);
```

```csharp
float3 RotateCubemap(float3 direction, float3 rotationAngles)
{
    // 获取绕 X、Y、Z 轴的旋转角度
    float angleX = radians(rotationAngles.x);
    float angleY = radians(rotationAngles.y);
    float angleZ = radians(rotationAngles.z);

    // 构造旋转矩阵
    float3x3 rotationMatrixX = float3x3(
        1, 0, 0,
        0, cos(angleX ), -sin(angleX ),
        0, sin(angleX ), cos(angleX )
    );

    float3x3 rotationMatrixY = float3x3(
        cos(angleY), 0, sin(angleY),
        0, 1, 0,
        -sin(angleY), 0, cos(angleY)
    );

    float3x3 rotationMatrixZ = float3x3(
        cos(angleZ), -sin(angleZ), 0,
        sin(angleZ), cos(angleZ), 0,
        0, 0, 1
    );

    direction = mul(rotationMatrixZ, direction);
    direction = mul(rotationMatrixY, direction);
    direction = mul(rotationMatrixX, direction);

    return direction;
}
```

---

- 虹膜与巩膜区域可能会存在不一样的反射强度，所以单独添加两个`reflection intensity` 变量来控制

```csharp
half4 irisReflection = reflectionTex * _IrisReflectionIntensity;
half4 scleraReflection = reflectionTex * _ScleraReflectionIntensity;
```

<img
	src='/assets/Effects/Eyeball/img10.png'
	alt='img10'
	style='width: 100%; height: auto; display:block; margin: 0 auto;'
/>

---

**Blinn-Phong镜面反射**

- 为了给虹膜区域添加更集中的高光，可以选择叠加Blinn-Phong高光

```csharp
// Blinn-Phong补充高光点
float3 halfDir = normalize(lightDir + viewDirWS);
float NdotH = saturate(dot(normalWS, halfDir));
float specular = pow(NdotH, _SpecularPower) * _SpecularIntensity;
```

<img
	src='/assets/Effects/Eyeball/img11.png'
	alt='img11'
	style='width: 100%; height: auto; display:block; margin: 0 auto;'
/>

<br></br>
#### 瞳孔与虹膜缩放

```csharp
    // 虹膜瞳孔缩放 -------------------------------------
    float2 remappedUV = RemapUV(IN.uv);
    half4 irisTex = SAMPLE_TEXTURE2D(_IrisTex, sampler_IrisTex, remappedUV);
    half4 irisColor = irisTex * _IrisColor  + irisReflection + specular;
```

- 额外定义`RamapUV()`函数来实UV*缩放*与*重映射*

```csharp
float2 RemapUV(float2 uv)
{
    // uv中心移动至原点（0，0）
    float2 transformedUV = uv - float2(0.5, 0.5);
    // 缩放
    float2 scaledUV = transformedUV / _IrisRadius;

    // Iris内UV重映射
    float lengthUV = length(scaledUV);

    float remappedPupilRadius = _PupilRadius * 0.5;
    float newLengthUV;

    // 黑色瞳孔内区域
    if (lengthUV <= remappedPupilRadius)
    {
        newLengthUV = lengthUV / remappedPupilRadius * _PupilRatio * 0.5;
    }
    else if (remappedPupilRadius < lengthUV  && lengthUV <= 0.5)
    {
        newLengthUV = (lengthUV - remappedPupilRadius) / (0.5 - remappedPupilRadius) * (0.5 - _PupilRatio * 0.5) + (_PupilRatio*0.5);
    }
    else
    {
        newLengthUV = lengthUV;
    }

    // newLengthUV / lengthUV 表示变化比例，用来乘以scaled UV得到按比例映射后的UV
    float2 remappedUV = scaledUV * (newLengthUV / lengthUV);
    return remappedUV + float2(0.5,0.5);
}

```

<LocalVideo src='/assets/Effects/Eyeball/video2.mp4' alt='video1' />

<br></br>
#### 完整代码

<details>
  <summary>*点此展开完整代码*</summary>
  ```csharp
  Shader "Custom/URPShaderTemplate"
{
    Properties
    {
        _IrisColor ("Iris Color", Color) = (1, 1, 1, 1)
        [NoScaleOffset]_IrisTex ("Iris Tex", 2D) = "white" {}
        [NoScaleOffset]_ScleraTex ("Sclera Tex", 2D) = "white" {}
        [NoScaleOffset]_IrisMask ("Iris Mask", 2D) = "white" {}
        _IrisRadius ("IrisRadius", Range(0.1,0.8)) = 0.5
        _PupilRadius ("Pupil Radius", Range(0,1)) = 0.14
        [NoScaleOffset]_CubeMap("CubeMap", CUBE) = "white"{}
        _IrisReflectionIntensity("Iris Reflection Intensity", Range(0,1)) = 0.5
        _ScleraReflectionIntensity("Sclera Reflection Intensity", Range(0,1)) = 0.5
        _ReflectionRotation ("Reflection Rotation (X, Y, Z)", Vector) = (0, 0, 0, 0)
        _SpecularPower("Specular Power", Range(300,800)) = 700
        _SpecularIntensity("Specular Intensity", Range(0,1)) = 0.5
    }

    SubShader
    {
        Tags { "RenderType"="Opaque" "RenderPipeline"="UniversalRenderPipeline" }

        Pass
        {
            Tags { "LightMode"="UniversalForward" }

            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Input.hlsl"
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"

            TEXTURE2D(_IrisTex);    SAMPLER(sampler_IrisTex);
            TEXTURE2D(_IrisMask);    SAMPLER(sampler_IrisMask);
            TEXTURE2D(_ScleraTex);    SAMPLER(sampler_ScleraTex);
            TEXTURECUBE(_CubeMap);    SAMPLER(sampler_CubeMap);


            CBUFFER_START(UnityPerMaterial)
            float4 _IrisColor;
            float4 _IrisTex_ST;
            float _IrisRadius, _PupilRadius = 0.14, _PupilRatio, _IrisReflectionIntensity, _ScleraReflectionIntensity;
            float _SpecularIntensity, _SpecularPower;
            float3 _ReflectionRotation;
            CBUFFER_END

            struct Attributes
            {
                float4 positionOS : POSITION;
                float2 uv : TEXCOORD0;
                float3 normal : NORMAL;
            };

            struct Varyings
            {
                float4 positionHCS : SV_POSITION;
                float2 uv : TEXCOORD0;
                float3 positionWS   : TEXCOORD1;
                float3 normalWS     : TEXCOORD2;
                // float3 viewDirWS    : TEXCOORD3;
            };

            Varyings vert(Attributes IN)
            {
                Varyings OUT;
                OUT.positionHCS = TransformObjectToHClip(IN.positionOS.xyz);
                OUT.uv = TRANSFORM_TEX(IN.uv, _IrisTex);
                OUT.positionWS = TransformObjectToWorld(IN.positionOS.xyz);
                OUT.normalWS = TransformObjectToWorldNormal(IN.normal);
                return OUT;
            }

            float2 RemapUV(float2 uv)
            {
                // uv中心移动至原点（0，0）
                float2 transformedUV = uv - float2(0.5, 0.5);
                // 缩放
                float2 scaledUV = transformedUV / _IrisRadius;

                // Iris内UV重映射
                float lengthUV = length(scaledUV);

                float remappedPupilRadius = _PupilRadius * 0.5;
                float newLengthUV;

                // 黑色瞳孔内区域
                if (lengthUV <= remappedPupilRadius)
                {
                   newLengthUV = lengthUV / remappedPupilRadius * _PupilRatio * 0.5;
                }
                else if (remappedPupilRadius < lengthUV  && lengthUV <= 0.5)
                {
                    newLengthUV = (lengthUV - remappedPupilRadius) / (0.5 - remappedPupilRadius) * (0.5 - _PupilRatio * 0.5) + (_PupilRatio*0.5);
                }
                else
                {
                    newLengthUV = lengthUV;
                }

                // newLengthUV / lengthUV 表示变化比例，用来乘以scaled UV得到按比例映射后的UV
                float2 remappedUV = scaledUV * (newLengthUV / lengthUV);
                return remappedUV + float2(0.5,0.5);
            }

            float3 RotateCubemap(float3 direction, float3 rotationAngles)
            {
                // 获取绕 X、Y、Z 轴的旋转角度
                float angleX = radians(rotationAngles.x);
                float angleY = radians(rotationAngles.y);
                float angleZ = radians(rotationAngles.z);

                // 构造旋转矩阵
                float3x3 rotationMatrixX = float3x3(
                    1, 0, 0,
                    0, cos(angleX ), -sin(angleX ),
                    0, sin(angleX ), cos(angleX )
                );

                float3x3 rotationMatrixY = float3x3(
                    cos(angleY), 0, sin(angleY),
                    0, 1, 0,
                    -sin(angleY), 0, cos(angleY)
                );

                float3x3 rotationMatrixZ = float3x3(
                    cos(angleZ), -sin(angleZ), 0,
                    sin(angleZ), cos(angleZ), 0,
                    0, 0, 1
                );

                direction = mul(rotationMatrixZ, direction);
                direction = mul(rotationMatrixY, direction);
                direction = mul(rotationMatrixX, direction);

                return direction;
            }



            half4 frag(Varyings IN) : SV_Target
            {
                half4 color;

                // 漫反射 -------------------------------------
                float3 normalWS = normalize(IN.normalWS);
                Light mainLight = GetMainLight();
                float3 lightDir = normalize(mainLight.direction);
                float NdotL = saturate(dot(normalWS, lightDir)) * 0.5 + 0.5;
                float3 diffuse = mainLight.color * NdotL;

                // 镜面反射 -------------------------------------
                // Cubemap
                half3 viewDirWS = normalize(_WorldSpaceCameraPos.xyz - IN.positionWS.xyz);
                half3 reflectionUV = reflect(-viewDirWS, IN.normalWS);
                reflectionUV = RotateCubemap(reflectionUV, _ReflectionRotation);
                half4 reflectionTex = SAMPLE_TEXTURECUBE(_CubeMap,sampler_CubeMap,reflectionUV);
                half4 irisReflection = reflectionTex * _IrisReflectionIntensity;
                half4 scleraReflection = reflectionTex * _ScleraReflectionIntensity;


                // Blinn-Phong补充高光点
                float3 halfDir = normalize(lightDir + viewDirWS);
                float NdotH = saturate(dot(normalWS, halfDir));
                float specular = pow(NdotH, _SpecularPower) * _SpecularIntensity;

                // 虹膜瞳孔缩放 -------------------------------------
                float2 remappedUV = RemapUV(IN.uv);
                half4 irisTex = SAMPLE_TEXTURE2D(_IrisTex, sampler_IrisTex, remappedUV);
                half4 irisColor = irisTex * _IrisColor  + irisReflection + specular;

                half4 scleraTex = SAMPLE_TEXTURE2D(_ScleraTex, sampler_ScleraTex, IN.uv);
                half4 scleraColor = scleraTex + scleraReflection;

                float irisMask = SAMPLE_TEXTURE2D(_IrisMask, sampler_IrisMask, remappedUV).r;
                color.xyz = lerp(scleraColor.rgb, irisColor.rgb, irisMask);

                color.xyz *= diffuse;

                return color;
            }
            ENDHLSL
        }
    }

}

```
</details>

<LocalVideo src='/assets/Effects/Eyeball/video3.mp4' alt='video1' />


### 相关资源

Unity眼球shader笔记
- https://www.jianshu.com/p/9b7e40886ebf

渲染TA实战：眼球的渲染
- https://zhuanlan.zhihu.com/p/575949429

剖析Unreal Engine超真实人类的渲染技术 - 眼球渲染
- https://www.cnblogs.com/timlly/p/11144950.html

Next Generation Character Rendering
- https://www.iryoku.com/stare-into-the-future/
```
